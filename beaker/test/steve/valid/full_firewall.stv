foreign def puts(c : char[]) -> int;
foreign def printf(fmt : char[] , i : uint) -> int;
foreign def fprintf(fp : uint(8)&, fmt : char[], i : uint) -> int;
foreign def fopen(filename : char[], mode : char[]) -> uint(8)&;
foreign def fclose(uint(8)&) -> int;

// A stateless firewall / packet filtering application.
// Forwards packets like any router, except it will filter out all packets
// that do not go to certain ports. In this case, filter out all non-HTTP ports.
// Only TCP/UDP port 80 and 443 will be allowed through.

layout eth
{
  dst : uint(48);
  src : uint(48);
  type : uint(16);
}


layout ipv4
{
  version_ihl : uint(8);
  dscp_ecn    : uint(8);
  len         : uint(16);
  id          : uint(16);
  fragment    : uint(16);
  ttl         : uint(8);
  protocol    : uint(8);
  checksum    : uint(16);
  src         : uint(32);
  dst         : uint(32);
  // Ignore options.
}


layout udp
{
  src      : uint(16);
  dst      : uint(16);
  len      : uint(16);
  checksum : uint(16);
}


layout tcp
{
  src : uint(16);
  dst : uint(16);
  ack : uint(32);
  seq : uint(32);
  control : uint(16); // TCP len: 0-3, Reserved: 4-9, Flags: 10-15
  window : uint(16);
  checksum : uint(16);
  urgent_ptr : uint(16);
  // Ignore options.
}


decoder start eth_d(eth)
{
  extract eth.dst;
  extract eth.src;
  extract eth.type;

  // Check for IPv4. Ignore IPv6 for now.
  match(eth.type)
  {
    case 0x800: decode ipv4_d;
  }
  // Don't worry about other kinds of packets.
}


decoder ipv4_d(ipv4)
{
  // We actually need all fields to confirm the checksum.
  extract ipv4.version_ihl; // Use this to get header length.
  extract ipv4.dscp_ecn;
  extract ipv4.len;
  extract ipv4.id;
  extract ipv4.fragment;
  extract ipv4.ttl;       // Use time-to-live to decrement
  extract ipv4.protocol;
  extract ipv4.checksum;
  extract ipv4.src;
  extract ipv4.dst;

  // Calculate a checksum.
  var checksum : uint(16) =
     ipv4_checksum(ipv4.version_ihl, ipv4.dscp_ecn, ipv4.len, ipv4.id,
                   ipv4.fragment, ipv4.ttl, ipv4.protocol, ipv4.src, ipv4.dst);

  // Check the checksum against the header's checksum.
  if (checksum != ipv4.checksum)
    drop;

  // Drop time-to-live expired packets.
  if (ipv4.ttl == 0)
    drop;

  set ipv4.ttl = ipv4.ttl - 1; // Decrement ttl.

  // We've changed the ttl, we must set a new checksum.
  set ipv4.checksum =
    ipv4_checksum(ipv4.version_ihl, ipv4.dscp_ecn, ipv4.len, ipv4.id,
                  ipv4.fragment, ipv4.ttl, ipv4.protocol, ipv4.src, ipv4.dst);

  var hdr_len : uint(8) =  (ipv4.version_ihl & 0x0f) * 4;

  // Only care about udp and tcp requests,
  match (ipv4.protocol) {
    case 0x06: decode tcp_d advance hdr_len;
    case 0x11: decode udp_d advance hdr_len;
  }
}

decoder udp_d(udp)
{
  extract udp.src;
  extract udp.dst;
  extract udp.len;
  extract udp.checksum;

  goto udp_filter;
}


decoder tcp_d(tcp)
{
  extract tcp.src;
  extract tcp.dst;
  extract tcp.ack;
  extract tcp.seq;
  extract tcp.control;
  extract tcp.window;
  extract tcp.checksum;
  extract tcp.urgent_ptr;

  goto tcp_filter;
}


// Only route if the port# is 80 or 443
// Log the ip src address of all blocked and all allowed.
exact_table udp_filter(udp.dst)
  requires(ipv4.src)
{
  { 80 } ->
  {
    goto learn;
  }

  { 443 } ->
  {
    goto learn;
  }

  miss ->
  {
    drop;
  }
}


// Only route if the port# is 80 or 443
// Log the IP address of all blocked and all allowed.
exact_table tcp_filter(tcp.dst)
  requires(ipv4.src)
{
  { 80 } ->
  {
    goto learn;
  }

  { 443 } ->
  {
    goto learn;
  }

  miss ->
  {
    drop;
  }
}


// This event handles the actual "learning."
event learn_ip
  requires(ipv4.src) // It will learn src IP addresses.
{
  // This first entry prevents the same address from causing
  // this event twice. It sends the packet straight to routing.
  insert into learn
  { ipv4.src } -> { goto route; };

  // This establishes the IP address to port mapping.
  // Any packet whose dst address matches the current packet's
  // src address will be forwarded to the current packet's
  // ingress port.
  insert into route
  { ipv4.src } ->
  [egress = in_port]
  {
    output egress;
  };
}


// Learning table.
exact_table learn(ipv4.src)
{
  miss ->
  {
    raise learn_ip;
    goto route;
  }
}


// This ultimately decides where to forward packets based on
// their destination IP.
exact_table route(ipv4.dst)
  requires(ipv4.ttl)
{
  miss -> {
    output flood; // Flood on all unlearned addresses.
  }
}


def ipv4_checksum(vihl : uint(8), dscp_ecn : uint(8), len : uint(16),
                  id : uint(16), frag : uint(16), ttl : uint(8), proto : uint(8),
                  src : uint(32), dst : uint(32) ) -> uint(16)
{
  // Merge fields into 16-bit values.
  var merge1 : uint(16) = vihl << 8;
  merge1 = merge1 | dscp_ecn;

  var merge2 : uint(16) = ttl << 8;
  merge2 = merge2 | proto;

  // Split fields int 16-bit values.
  var split_src1 : uint(16) = src >> 16; // Get the upper 16 bits.
  // Get the lower 16 bits.
  var split_src2 : uint(16) = src & 0x0000_ffff;

  // Get the upper 16 bits.
  var split_dst1 : uint(16) = dst >> 16;
  // Get the lower 16 bits.
  var split_dst2 : uint(16) = dst & 0x0000_ffff;

  // Calculate the checksum, ignoring the actual checksum field which is treated
  // as 0.
  // Accumulated sum.
  var acc : uint(32) = 0;
  acc = acc + merge1 + len + id + frag + merge2 +
        split_src1 + split_src2 + split_dst1 + split_dst2;

  // Perform the 1's complement sum wraparound.
  var acc1 : uint(16) = acc >> 16; // Get the upper 16 bits.
  var acc2 : uint(16) = acc & 0x0000_ffff; // Lower 16 bits of acc

  acc2 = acc1 + acc2;
  acc2 = acc2 ^ 0xffff_ffff;
  return acc2;
}
