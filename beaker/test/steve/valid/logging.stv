foreign def printf(fmt : char[], ...) -> int;
foreign def fprintf(fp : uint(8)&, fmt : char[], ...) -> int;
foreign def fopen(filename : char[], mode : char[]) -> uint(8)&;
foreign def fclose(uint(8)&) -> int;

layout eth
{
  dst : uint(48);
  src : uint(48);
  type : uint(16);
}


layout ipv4
{
  version_ihl : uint(8);
  dscp_ecn    : uint(8);
  len         : uint(16);
  id          : uint(16);
  fragment    : uint(16);
  ttl         : uint(8);
  protocol    : uint(8);
  checksum    : uint(16);
  src         : uint(32);
  dst         : uint(32);
  // Ignore options.
}


layout udp
{
  src      : uint(16);
  dst      : uint(16);
  len      : uint(16);
  checksum : uint(16);
}


layout tcp
{
  src : uint(16);
  dst : uint(16);
  ack : uint(32);
  seq : uint(32);
  control : uint(16); // TCP len: 0-3, Reserved: 4-9, Flags: 10-15
  window : uint(16);
  checksum : uint(16);
  urgent_ptr : uint(16);
  // Ignore options.
}


decoder start eth_d(eth)
{
  extract eth.type;
  match(eth.type) {
    case 0x800: decode ipv4_d;
  }
}


decoder ipv4_d(ipv4)
{
  extract ipv4.version_ihl;
  extract ipv4.protocol;
  extract ipv4.src;
  extract ipv4.dst;

  var hdr_len : uint(8) =  (ipv4.version_ihl & 0x0f) * 4;

  // Only care about udp and tcp requests,
  match (ipv4.protocol) {
    case 0x06: decode tcp_d advance hdr_len;
    // case 0x11: decode udp_d advance hdr_len;
  }
}

decoder udp_d(udp)
{
  extract udp.dst;
  goto udp_filter;
}


decoder tcp_d(tcp)
{
  extract tcp.dst;
  goto tcp_filter;
}


exact_table udp_filter(udp.dst) requires(ipv4.src)
{
  { 80 } -> { raise log_allowed_tcp; }
  // miss -> { raise log_blocked_tcp; }
}


// Only route if the port# is 80 or 443
// Log the IP address of all blocked and all allowed.
exact_table tcp_filter(tcp.dst)
  requires(ipv4.src)
{
  { 80 } -> { raise log_allowed_tcp; }
  // miss -> { raise log_blocked_tcp; }
}


event log_allowed_udp requires(ipv4.src, udp.dst)
{
  var fp : uint(8)& = fopen("allowed.txt", "a");
  fprintf(fp, "Allowing address: %x\n", ipv4.src);
  fclose(fp);
}


event log_blocked_udp requires(ipv4.src, udp.dst)
{
  var fp : uint(8)& = fopen("blocked.txt", "a");
  fprintf(fp, "Blocking address: %x\n", ipv4.src);
  fclose(fp);
}


event log_allowed_tcp requires(ipv4.src, tcp.dst)
{
  var fp : uint(8)& = fopen("allowed.txt", "a");
  fprintf(fp, "Allowing address: %x\n", ipv4.src);
  fclose(fp);
}


event log_blocked_tcp requires(ipv4.src, tcp.dst)
{
  var fp : uint(8)& = fopen("blocked.txt", "a");
  fprintf(fp, "Blocking address: %x\n", ipv4.src);
  fclose(fp);
}
